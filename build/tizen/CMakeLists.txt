CMAKE_MINIMUM_REQUIRED(VERSION 3.8.2)

SET(CMAKE_C_STANDARD 99)
SET(CMAKE_CXX_STANDARD 17)
PROJECT(dali-example C CXX)

SET(dali-example_VERSION_MAJOR 1)
SET(dali-example_VERSION_MINOR 0)

SET(ROOT_SRC_DIR ${CMAKE_SOURCE_DIR}/../..)
GET_FILENAME_COMPONENT(ROOT_SRC_DIR ${ROOT_SRC_DIR} ABSOLUTE) # Make sure the path is absolute

SET(PREFIX ${CMAKE_INSTALL_PREFIX})

# Directory Setup
IF(DEFINED DALI_APP_DIR)
  SET(BINDIR ${DALI_APP_DIR}/bin)
ELSE()
  SET(BINDIR ${PREFIX}/bin)
ENDIF()

IF(DEFINED DALI_APP_RES_DIR)
  SET(APP_DATA_RES_DIR ${DALI_APP_RES_DIR})
ELSE()
  SET(APP_DATA_RES_DIR ${PREFIX}/share/com.samsung.dali-example/res)
ENDIF()

SET(RESOURCE_DIR ${ROOT_SRC_DIR}/resources)
SET(LOCAL_IMAGES_DIR ${RESOURCE_DIR}/images)
SET(LOCAL_STYLE_DIR ${RESOURCE_DIR}/style)

SET(IMAGES_DIR ${APP_DATA_RES_DIR}/images/)
SET(STYLE_DIR ${APP_DATA_RES_DIR}/style/)

SET(DEMO_IMAGE_DIR ${IMAGES_DIR})
SET(DEMO_STYLE_DIR "${STYLE_DIR}")
SET(DEMO_LANG "${LANG}")

# Image Resources
INSTALL(DIRECTORY ${LOCAL_IMAGES_DIR} DESTINATION ${APP_DATA_RES_DIR})

# Parse json.in files by replacing @DEMO_IMAGE_DIR@ in following files & setting the installation path
FILE(GLOB INPUT_STYLE_FILES "${LOCAL_STYLE_DIR}/*.json.in")
FOREACH(FILE ${INPUT_STYLE_FILES})
  GET_FILENAME_COMPONENT(FILE_NAME_ONLY ${FILE} NAME_WE)
  SET(JSON_FILE "${FILE_NAME_ONLY}.json")
  CONFIGURE_FILE( ${FILE} ${JSON_FILE} )
  INSTALL(FILES ${JSON_FILE} DESTINATION ${STYLE_DIR})
ENDFOREACH(FILE)

# Create resources location file
CONFIGURE_FILE( resources-location.in resources-location.cpp )

# Dependency Checks
SET(PKG_LIST dali2-core
             dali2-adaptor
             dali2-toolkit)

INCLUDE(FindPkgConfig)
PKG_CHECK_MODULES(REQUIRED_PKGS REQUIRED ${PKG_LIST})

FOREACH(flag ${REQUIRED_PKGS_CFLAGS})
  SET(REQUIRED_CFLAGS "${REQUIRED_CFLAGS} ${flag}")
ENDFOREACH(flag)

# Build Options
SET(DALI_EXAMPLE_CFLAGS "-DDEMO_IMAGE_DIR=\\\"${DEMO_IMAGE_DIR}\\\" -DDEMO_STYLE_DIR=\\\"${DEMO_STYLE_DIR}\\\" -fvisibility=hidden -DHIDE_DALI_INTERNALS -DDEMO_LANG=\\\"${DEMO_LANG}\\\"")

if(CMAKE_BUILD_TYPE MATCHES Debug)
  MESSAGE(STATUS "CMAKE_BUILD_TYPE: " Debug)
  SET(DALI_EXAMPLE_CFLAGS "${DALI_EXAMPLE_CFLAGS} -DDEBUG_ENABLED")
ELSE()
  MESSAGE(STATUS "CMAKE_BUILD_TYPE: " Release)
ENDIF()

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${REQUIRED_CFLAGS} ${DALI_EXAMPLE_CFLAGS} -Werror -Wall -fPIE")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${CMAKE_C_FLAGS}")

INCLUDE_DIRECTORIES(${ROOT_SRC_DIR}/src)

# Setup for dali-example
SET(EXAMPLE_SRC_DIR ${ROOT_SRC_DIR}/src)

AUX_SOURCE_DIRECTORY(${EXAMPLE_SRC_DIR} EXAMPLE_SRCS)
SET(EXAMPLE_SRCS ${EXAMPLE_SRCS} resources-location.cpp )

ADD_EXECUTABLE(${PROJECT_NAME} ${EXAMPLE_SRCS})
TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${REQUIRED_PKGS_LDFLAGS} -pie)

INSTALL(TARGETS ${PROJECT_NAME} DESTINATION ${BINDIR})
